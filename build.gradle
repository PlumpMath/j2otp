apply plugin: 'java'

sourceCompatibility = 1.6
version = '1.0'

repositories {
    mavenCentral()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.11'
    compile 'gnu.getopt:java-getopt:1.0.13'
    compile 'org.erlang.otp:jinterface:1.5.6'
    compile 'org.apache.commons:commons-pool2:2.2'
}

jar {
    manifest {
        attributes(
                'Create-By': create_by(),
                'Build-By': "$System.env.USER@" + hostname(),
                'Build-Version': jar_version(),
                'Build-Date': new Date()
        )
    }
}

task uberjar(type: Jar, dependsOn:jar/*,dependsOn:[':compileJava',':compileGroovy'*/) {
    from files(sourceSets.main.output.classesDir)
    from configurations.runtime.asFileTree.files.collect { zipTree(it) }
    exclude "META-INF/*.SF"
    exclude "META-INF/*.RSA"

    manifest {
        attributes(
                'Create-By': System.getProperty('java.version'),
                'Main-Class': 'com.xw.push.Core',
                'Build-Version': jar_version(),
                'Build-Date': new Date()
        )
    }
}

String create_by() {
    String.format('%s %s',
            System.getProperty('java.version'),
            System.getProperty('java.vm.vendor')
    )
}

String hostname() {
    InetAddress.getLocalHost().getHostName()
}

String vcs_version() {
    'git describe --tags --always HEAD'.execute().text.trim()
}

String jar_version() {
    String.format('%s-%s', version, vcs_version())
}